apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'com.getkeepsafe.dexcount'

android {
    signingConfigs {
        debug {
            keyAlias 'debug'
            keyPassword 'kVTcnV78DMu6'
            storeFile file('../.debug.jks')
            storePassword 'kVTcnV78DMu6'
        }
        release {
            keyAlias 'debug'
            keyPassword 'kVTcnV78DMu6'
            storeFile file('../.debug.jks')
            storePassword 'kVTcnV78DMu6'
        }
    }

    compileSdkVersion 28

    defaultConfig {
        applicationId "com.mononz.crypz"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode (System.getenv('CIRCLE_BUILD_NUM') != null ? Integer.valueOf(System.getenv('CIRCLE_BUILD_NUM')) : 1)
        versionName "1.0.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            //applicationIdSuffix ".beta"
            debuggable true
            signingConfig signingConfigs.debug
            shrinkResources false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-rules-dev.pro'
        }
        release {
            debuggable false
            signingConfig signingConfigs.release
            shrinkResources false
            minifyEnabled true  // Deobfuscation mapping file located at app/build/outputs/mapping/release/
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFile 'proguard-rules-test.pro'
        }
    }

    buildTypes.each {
        it.buildConfigField 'String', 'BASIC_AUTH',  '"Basic 1qwer67574476547547636452654=="'
    }

    flavorDimensions 'mononz'

    productFlavors {
        dev {
            multiDexEnabled true
            dimension 'mononz'
            buildConfigField 'String', 'SERVER_PATH', '"http://192.168.0.16:3001/"'
            resValue "string", "application_name",  "Crypz Alpha"
        }
        staging {
            multiDexEnabled true
            dimension 'mononz'
            buildConfigField 'String', 'SERVER_PATH', '"https://api-crypz.mononz.com/"'
            resValue "string", "application_name",  "Crypz Beta"
        }
        prod {
            dimension 'mononz'
            buildConfigField 'String', 'SERVER_PATH', '"https://api-crypz.mononz.com/"'
            resValue "string", "application_name",  "Crypz"
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {

    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // DEV ONLY
    devImplementation 'com.facebook.stetho:stetho:1.5.0'                    // https://github.com/facebook/stetho/releases
    devImplementation 'com.facebook.stetho:stetho-okhttp3:1.5.0'
    devImplementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'     // https://github.com/square/okhttp/releases
    implementation 'com.jakewharton.timber:timber:4.7.0'                    // https://github.com/JakeWharton/timber/releases

    // Android
    implementation 'androidx.appcompat:appcompat:1.0.0-beta01'              // https://developer.android.com/topic/libraries/support-library/revisions.html
    implementation 'androidx.legacy:legacy-support-v4:1.0.0-beta01'         // update number below 'configurations.all'
    implementation 'com.google.android.material:material:1.0.0-beta01'
    implementation 'androidx.percentlayout:percentlayout:1.0.0-beta01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0-beta01'
    implementation 'androidx.gridlayout:gridlayout:1.0.0-beta01'
    implementation 'androidx.cardview:cardview:1.0.0-beta01'

    // Google
    implementation 'com.google.firebase:firebase-core:16.0.1'               // https://developers.google.com/android/guides/releases
    implementation 'com.google.firebase:firebase-database:16.0.1'
    implementation 'com.google.firebase:firebase-messaging:17.1.0'
    implementation 'com.google.firebase:firebase-perf:16.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'

    // Android Architecture
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.0.0-beta01' // https://developer.android.com/topic/libraries/architecture/adding-components.html
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0-beta01'
    kapt 'androidx.lifecycle:lifecycle-compiler:2.0.0-beta01'
    implementation 'androidx.room:room-runtime:2.0.0-beta01'
    kapt 'androidx.room:room-compiler:2.0.0-beta01'
    implementation 'androidx.room:room-rxjava2:2.0.0-beta01'

    // Reporting
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'          // https://fabric.io/kits/android/crashlytics/install')

    // Injection
    implementation 'com.google.dagger:dagger:2.16'                          // https://github.com/google/dagger/releases
    kapt 'com.google.dagger:dagger-compiler:2.16'
    implementation 'com.google.dagger:dagger-android:2.16'
    implementation 'com.google.dagger:dagger-android-support:2.16'
    kapt 'com.google.dagger:dagger-android-processor:2.16'

    // Network
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'                  // https://github.com/square/retrofit/releases
    implementation 'com.squareup.retrofit2:converter-moshi:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'
    implementation 'com.github.bumptech.glide:glide:4.8.0-SNAPSHOT'                  // https://github.com/bumptech/glide/releases
    kapt 'com.github.bumptech.glide:compiler:4.8.0-SNAPSHOT'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.8.0-SNAPSHOT'

    // Other Libraries
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'                   // https://github.com/ReactiveX/RxAndroid/releases
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'                    // https://github.com/ReactiveX/RxKotlin/releases
    implementation 'com.caverock:androidsvg:1.2.1'                          // https://github.com/BigBadaboom/androidsvg/releases
    implementation 'com.github.bmarrdev:android-DecoView-charting:v1.2'     // https://github.com/bmarrdev/android-DecoView-charting/releases

    // Testing Dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test:rules:1.1.0-alpha4'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha3', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

/*
All direct/transitive dependencies shared between your test and production APKs need to be
excluded from the test APK! This is necessary because both APKs will contain the same classes. Not
excluding these dependencies from your test configuration will result in an dex pre-verifier error
at runtime. More info in this tools bug: (https://code.google.com/p/android/issues/detail?id=192497)
*/
configurations.compile.dependencies.each { compileDependency ->
    println "Excluding compile dependency: ${compileDependency.getName()}"
    configurations.androidTestCompile.dependencies.each { androidTestCompileDependency ->
        configurations.androidTestCompile.exclude module: "${compileDependency.getName()}"
    }
}

apply plugin: 'com.google.gms.google-services'